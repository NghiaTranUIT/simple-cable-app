#!/usr/bin/env escript
%% -*- erlang -*-
%%! -sname rpc_bench -debug verbose -config bench.config

-mode(native).

-define(DF(X, Y), io:format(X ++ "~n", Y)).
-define(D(X), io:format(X ++ "~n")).

-define(CHANNEL, <<"{\"channel\":\"BenchmarkChannel\"}">>).
-define(MESSAGE, <<"{\"action\":\"echo\",\"text\":\"hello\"}">>).

-include("../../erlycable/include/anycable_pb.hrl").

main([Host, Port, Count, SecondsStr]) ->
  {ok, Libs} = file:list_dir("../../erlycable/_build/default/lib"),

  [code:add_path("../../erlycable/_build/default/lib/" ++ Lib ++ "/ebin") || Lib <- Libs],

  ulitos_app:ensure_started(erlgrpc),

  Seconds = list_to_integer(SecondsStr),

  Stats = spawn_link(fun() -> init_stats(Seconds) end),

  erlycable:start(),

  start_clients(#{
    count => list_to_integer(Count),
    stats => Stats,
    seconds => Seconds
  });

main(_) ->
    usage().

usage() ->
    ?D("usage: rpc_bench <host> <port> <count> <seconds>\n"),
    halt(1).

start_clients(Config) ->
  start_clients(Config, 0, #{}).

start_clients(#{ count := Count } = Config, Number, Clients) when Number < Count ->
  % ?DF("Starting client ~p", [Number+1]),
  {Pid, _} = spawn_monitor(fun() -> init_client(Config) end),
  receive
    {'EXIT', _Pid, Reason} ->
      ?DF("Failed to start client: ~p", [Reason]),
      halt(1)
  after
    100 ->
      start_clients(Config, Number + 1, maps:put(Pid, alive, Clients))
  end;

start_clients(#{ count := Count } = Config, Count, Clients) ->
  ?D("All started"),
  run_benchmarks(Config, Count, Clients).

run_benchmarks(#{ seconds := Seconds, stats := Stats } = Config, Count, Clients) ->
  ?DF("Running ~p benchmark clients for ~ps", [Count, Seconds]),
  timer:sleep(Seconds * 1000),
  
  ?D("Stopping clients..."),
  [Pid ! done || {Pid, _} <- maps:to_list(Clients)],

  flush_exits(),

  Stats ! {done, self()},

  receive
    ok -> ?D("Done!")
  after
    1000 ->
      ?D("Timeouted wating for stats"),
      halt(1)
  end.

flush_exits() ->
  receive
    {'DOWN', _Ref, process, Pid, normal} ->
      ?DF("Client done: ~p", [Pid]);
    {'DOWN', _Ref, process, Pid, _Reason} ->
      ?DF("Dead client ~p", [_Reason]);
    Else ->
      ?DF("Unknown message: ~p", [Else]),
      halt(1)
  end.

init_stats(Seconds) ->
  loop_stats(Seconds, 0, 0, 0).

loop_stats(Seconds, Count, Time, Errors) ->
  receive
    {init, Pid} -> loop_stats(Seconds, Count, Time, Errors);
    {call, _Pid, MicroSeconds} -> loop_stats(Seconds, Count + 1, Time + MicroSeconds, Errors);
    {error, _Pid, MicroSeconds} -> loop_stats(Seconds, Count + 1, Time + MicroSeconds, Errors + 1);
    {done, From} ->
        ?DF("Results: rps=~p count=~p errors=~p time=~p avg=~pus", [Count / Seconds, Count, Errors, Time, Time / Count]),
        From ! ok
  end.

init_client(#{ stats := Stats } = Config) ->
  %% Connect and subscribe to BenchmarkChannel
  {ok,  #'ConnectionResponse'{identifiers = Identifiers}} = erlycable_rpc:connect("/cable", []),

  {ok, _} = erlycable_rpc:subscribe(Identifiers, ?CHANNEL, []),

  Stats ! {init, self()},

  run_bench(Identifiers, Stats).

run_bench(Identifiers, Stats) ->
  self() ! run_bench,

  receive
    done ->
      ulitos:flush_box(),
      exit(normal);
    run_bench ->
      {Time, Result} = timer:tc(erlycable_rpc, perform, [Identifiers, ?CHANNEL, ?MESSAGE]),
      case Result of
        {ok, _} ->
          Stats ! {call, self(), Time};
        {error, _} ->
          Stats ! {error, self(), Time}
      end,
      run_bench(Identifiers, Stats)
  end.
